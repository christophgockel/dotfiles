"Rename tabs to show tab# and # of viewports
if exists("+showtabline")
  hi TabLineFill  ctermfg=20 ctermbg=19
  hi TabLine      ctermfg=20 ctermbg=18
  hi TabLineSel   ctermfg=15 ctermbg=33

  hi TabNum       ctermfg=20 ctermbg=18
  hi TabNumSel    ctermfg=15 ctermbg=33
  hi TabWinNum    ctermfg=20 ctermbg=18
  hi TabWinNumSel ctermfg=15 ctermbg=33

  function! TabLineWithIndexAndNumberOfSplits()
    let s = ''
    let wn = ''
    let t = tabpagenr()
    let i = 1
    while i <= tabpagenr('$')
      let buflist = tabpagebuflist(i)
      let winnr = tabpagewinnr(i)
      let s .= '%' . i . 'T'
      let s .= (i == t ? '%1*' : '%2*')
      let wn = tabpagewinnr(i,'$')

      let s .= (i== t ? '%#TabNumSel#' : '%#TabNum#')
      let s .= ' ' . i
      if tabpagewinnr(i,'$') > 1
        let s .= '.'
        let s .= (i== t ? '%#TabWinNumSel#' : '%#TabWinNum#')
        let s .= (tabpagewinnr(i,'$') > 1 ? wn : '')
      end

      let s .= '%*'
      let s .= (i == t ? '%#TabLineSel#' : '%#TabLine#')
      let bufnr = buflist[winnr - 1]
      let file = bufname(bufnr)
      let buftype = getbufvar(bufnr, 'buftype')
      if buftype == 'nofile'
        if file =~ '\/.'
          let file = substitute(file, '.*\/\ze.', '', '')
        endif
      else
        let file = fnamemodify(file, ':p:t')
      endif
      if file == ''
        let file = '[No Name]'
      endif
      let s .= ' ' . file . ' '
      let s .= (i == t ? '%m' : '')
      let i = i + 1
    endwhile
    let s .= '%T%#TabLineFill#%='
    return s
  endfunction
"  set stal=2
  set tabline=%!TabLineWithIndexAndNumberOfSplits()
endif
